plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id "com.google.cloud.tools.jib" version "3.3.1"
    id "com.vaadin" version "24.0.2"
}

group = 'com.thoroldvix'
version = '0.5.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url "https://maven.vaadin.com/vaadin-addons" }
}
jib {
    to {
        image = "registry.hub.docker.com/thorold/price-pal"
    }
}
ext {
    testContainers = "1.17.5"
    mapStruct = "1.5.3.Final"
    springCloudVersion = "2022.0.0"
    vaadinVersion = "24.0.2"
    apexCharts = "23.0.1"
    gridPagination = "4.0.0"
    caffeineCache = "3.1.6"
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-devtools'
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    implementation 'org.liquibase:liquibase-core'
    implementation "org.mapstruct:mapstruct:${mapStruct}"
    implementation "com.github.appreciated:apexcharts:${apexCharts}"
    implementation "org.vaadin.klaudeta:grid-pagination:${gridPagination}"
    implementation "com.github.ben-manes.caffeine:caffeine:${caffeineCache}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.3"


    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStruct}"
    testImplementation "org.testcontainers:postgresql:${testContainers}"
    testImplementation "org.testcontainers:junit-jupiter:${testContainers}"
}

test {
    useJUnitPlatform()
}